// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.0'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-rc03'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.0.0-RC11")
    id "org.jlleitschuh.gradle.ktlint" version "6.3.0"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: "$rootProject.projectDir/dependencies.gradle"

import org.jlleitschuh.gradle.ktlint.reporter.ReporterType
subprojects {

    apply {
        plugin("io.gitlab.arturbosch.detekt")
        plugin("org.jlleitschuh.gradle.ktlint")
    }

    def tools = rootProject.ext.tools

    detekt {
        toolVersion = tools.detektVersion
        filters = ".*/resources/.*,.*/build/.*"
        reports {
            xml.enabled = true
            html {
                enabled = true
                destination = file("$buildDir.absolutePath/reports/detekt.html")
            }
        }
    }

    ktlint {
        version = tools.ktlintVersion
        debug = true
        verbose = true
        android = true
        outputToConsole = true
        reporters = [ReporterType.CHECKSTYLE]
        ignoreFailures = true
        filter {
            exclude("**/generated/**")
            include("**/kotlin/**")
            include("**/java/**")
        }
    }
}
